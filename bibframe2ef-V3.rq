PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX htrc: <http://wcsa.htrc.illinois.edu/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ht: <https://www.hathitrust.org/>
PREFIX prism: <http://prismstandard.org/namespaces/basic/2.0/>
PREFIX astrs: <http://www.w3.org/ns/activitystreams#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix schema: <http://schema.org/>

CONSTRUCT {
	?item	a	schema:PublicationVolume ;
			schema:name	?title ;
			htrc:enumerationChronology	?enumChron ;
			schema:contributor	?Contrib ;
			schema:publisher	_:vPublisher ;
			schema:locationCreated	?place ;
			schema:datePublished	?str_date ;
			schema:genre	?genre ;
			schema:inLanguage	?str_lang ;
			htrc:accessRights	?rights ;
			schema:isAccessibleForFree	?isFree ;
			htrc:typeOfResource	?str_type ;
			schema:sourceInstitution	_:vSourceInst ;
			schema:mainEntityOfPage	?full_record ;
			schema:mainEntityOfPage	?brief_record ;
			schema:mainEntityOfPage	?htid_record ;
			schema:issn	?issn ;
			schema:isbn	?isbn ;
			schema:identifier	_:vOCLC ;
			schema:identifier	_:vLccn . 

	_:vPublisher	schema:name	?publisher ;
					rdf:type	<http://id.loc.gov/ontologies/bibframe/Organization> .

	_:vSourceInst	schema:name	?src_inst ;
					rdf:type	<http://id.loc.gov/ontologies/bibframe/Organization> .

	_:vOCLC	a	schema:PropertyValue ;
			schema:propertyID	"oclc" ;
			schema:value	?oclcNo .

	_:vLccn	a	schema:PropertyValue ;
			schema:propertyID	"lccn" ;
			schema:value	?lccn .

	?place	a	<http://id.loc.gov/ontologies/bibframe/Place> .

	?Contrib	schema:name	?Name ;
				rdf:type ?ContribType .

}
WHERE {
		
	?item	dcterms:title	?title  .
	?item	bf:itemOf	?Inst .
	?Inst	bf:instanceOf	?Work .

	OPTIONAL {	?item	dcterms:accessRights	?rights . }
	OPTIONAL {	?item	dcterms:accessRights	?ar . 
						FILTER (?ar = "pd")
						BIND ("TRUE" as ?isFree)
	}
	OPTIONAL {	?item	htrc:contentProviderAgent	?src . 
						BIND(SUBSTR(STR(?src),52) as ?src_inst)
	}
	OPTIONAL {	?item	bf:enumerationAndChronology	?enumChron . }
	BIND (CONCAT(CONCAT('https://catalog.hathitrust.org/api/volumes/full/recordnumber/',SUBSTR(STR(?Inst),30)),'.json') as ?full_record)
	BIND (CONCAT(CONCAT('https://catalog.hathitrust.org/api/volumes/brief/recordnumber/',SUBSTR(STR(?Inst),30)),'.json') as ?brief_record)
	BIND (CONCAT(CONCAT('https://catalog.hathitrust.org/api/volumes/full/htid/',SUBSTR(STR(?item),28)),'.json') as ?htid_record)

	OPTIONAL {	?Inst	bf:issuance	?genre . }
	OPTIONAL {	?Inst		bf:provisionActivity	?provAct . 
				?provAct	bf:date	?date .
							filter ( datatype(?date) = <http://id.loc.gov/datatypes/edtf> )
							BIND (STR(?date) as ?str_date )
	}
	OPTIONAL {	?Inst		bf:provisionActivity	?provAct2 . 
				?provAct2	bf:agent/rdfs:label	?publisher .
	}
	OPTIONAL {	?Inst		bf:provisionActivity	?provAct3 . 
				?provAct3	bf:place	?place .
							FILTER(!isBlank(?place))
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Issn .
				?Issn	rdf:value	?issn . 
				?Issn	a	?issn_class .
						FILTER( ?issn_class = <http://id.loc.gov/ontologies/bibframe/Issn> )
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Isbn .
				?Isbn	rdf:value	?isbn . 
				?Isbn	a	?isbn_class .
						FILTER( ?isbn_class = <http://id.loc.gov/ontologies/bibframe/Isbn> )
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Lccn .
				?Lccn	rdf:value	?lccn . 
				?Lccn	a	?lccn_class .
						FILTER( ?lccn_class = <http://id.loc.gov/ontologies/bibframe/Lccn> )
	}
	OPTIONAL {	?Inst	bf:identifiedBy ?local .
				?local	rdf:value ?oclcNo ;
						bf:source/rdfs:label ?so . 
						filter ( ?so = "OCoLC" ) 
	}

	OPTIONAL {	?Work	bf:language	?lang . 
						BIND(SUBSTR(STR(?lang),40) as ?str_lang)
	}
	OPTIONAL {	?Work	rdf:type	?type . 
						filter ( ?type != <http://id.loc.gov/ontologies/bibframe/Work> )
						BIND(STR(?type) as ?str_type)
	}
	OPTIONAL {	?Work	bf:contribution/bf:agent	?Contrib .
				?Contrib	rdfs:label	?Name ;
							rdf:type	?ContribType . 
							filter (?ContribType NOT IN (bf:Agent) ) 
	}

	VALUES (?item) {
		( ?handle_url )
	}
}