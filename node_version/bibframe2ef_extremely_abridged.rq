PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX htrc: <http://wcsa.htrc.illinois.edu/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ht: <https://www.hathitrust.org/>
PREFIX prism: <http://prismstandard.org/namespaces/basic/2.0/>
PREFIX astrs: <http://www.w3.org/ns/activitystreams#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix schema: <http://schema.org/>

CONSTRUCT {
	?item	a	?iss ;
			htrc:volume_name	?title ;
			htrc:enumerationChronology	?enumChron ;
			schema:datePublished	?int_date ;
			schema:genre	?genre ;
			schema:inLanguage	?str_lang ;
			schema:inLanguage	?node_str_lang ;
			htrc:accessRights	?rights ;
			schema:isAccessibleForFree	?isFree ;
			htrc:typeOfResource	?str_type ;
			schema:provider		?src ;
			schema:isPartOf	?part_of ;
			schema:mainEntityOfPage	?full_record ;
			schema:mainEntityOfPage	?brief_record ;
			schema:mainEntityOfPage	?htid_record ;
			schema:issn	?issn_value ;
			schema:isbn	?isbn_value ;
			schema:identifier	?oclc_node ;
			schema:identifier	?Lccn . 

	?place	a	<http://id.loc.gov/ontologies/bibframe/Place> .

	?src	a	<http://id.loc.gov/ontologies/bibframe/Organization> ;
			schema:name ?src_inst .

	?oclc_node	a	schema:PropertyValue ;
				schema:propertyID	"oclc" ;
				schema:value	?oclcNo .

	?Lccn	a	schema:PropertyValue ;
			schema:propertyID	"lccn" ;
			schema:value	?lccn_str .
}
WHERE {
		
	?item	dcterms:title	?title  .
	?item	htrc:hasFullDescription	?full_record .
	?item	htrc:hasBriefDescription	?brief_record .
	?item	htrc:hasCatalogDescription	?htid_record .
	?item	bf:itemOf	?Inst .
	?Inst	bf:instanceOf	?Work .

	OPTIONAL {	?item	dcterms:accessRights	?rights . }
	OPTIONAL {	?item	bf:enumerationAndChronology	?enumChron . }
	OPTIONAL {	?item	dcterms:accessRights	?ar . 
						FILTER (?ar = "pd")
						BIND ('true' as ?isFree)
	}
	OPTIONAL {	?item	dcterms:accessRights	?ar . 
						FILTER (?ar != "pd")
						BIND ('false' as ?isFree)
	}
	OPTIONAL {	?item	htrc:contentProviderAgent	?src . 
						BIND(SUBSTR(STR(?src),52) as ?src_inst)
	}
	OPTIONAL {	?item	dcterms:created	?date .
						BIND(SUBSTR(STR(?date),1,4) as ?int_date )
	}

	OPTIONAL {	?Inst	bf:issuance	?issuance .
						FILTER (SUBSTR(STR(?issuance),39) = 'mono')
						BIND(<http://schema.org/Book> as ?iss)
	}
	OPTIONAL {	?Inst	bf:issuance	?issuance2 .
						FILTER (SUBSTR(STR(?issuance2),39) = 'serl')
						BIND(<http://schema.org/PublicationVolume> as ?iss)
	}
	OPTIONAL {	?Inst	bf:issuance	?issuance3 .
						FILTER (SUBSTR(STR(?issuance3),39) != 'serl' && SUBSTR(STR(?issuance3),39) != 'mono' )
						BIND(<http://schema.org/CreativeWork> as ?iss)
	}
	OPTIONAL {	?Inst	bf:issuance	?issuance4 .
						FILTER (SUBSTR(STR(?issuance4),39) = 'serl')
						BIND(?Inst as ?part_of)
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Issn .
				?Issn	rdf:value	?issn_value . 
				?Issn	a	?issn_class .
						FILTER( ?issn_class = <http://id.loc.gov/ontologies/bibframe/Issn> )
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Isbn .
				?Isbn	rdf:value	?isbn_value . 
				?Isbn	a	?isbn_class .
						FILTER( ?isbn_class = <http://id.loc.gov/ontologies/bibframe/Isbn> )
	}
	OPTIONAL {	?Inst	bf:identifiedBy	?Lccn .
				?Lccn	rdf:value	?lccn . 
				?Lccn	a	?lccn_class .
						FILTER( ?lccn_class = <http://id.loc.gov/ontologies/bibframe/Lccn> )
						BIND(STR(?lccn) as ?lccn_str)
	}
	OPTIONAL {	?Inst	bf:issuance	?oclc_node . 
						FILTER(SUBSTR(STR(?Inst),1,1) != "_")
						BIND(SUBSTR(STR(?Inst),30) as ?oclcNo)
	}

	OPTIONAL {	?Work	bf:genreForm	?genre . }
	OPTIONAL {	?Work	bf:language	?lang . 
						FILTER(!ISBLANK(?lang)) 
						BIND(SUBSTR(STR(?lang),40) as ?str_lang)
	}
	OPTIONAL { ?Work	bf:language/bf:identifiedBy/rdf:value	?node_lang . 
						BIND(SUBSTR(STR(?node_lang),40) as ?node_str_lang)
	}
	OPTIONAL {	?Work	rdf:type	?type . 
						filter ( ?type != <http://id.loc.gov/ontologies/bibframe/Work> )
						BIND(STR(?type) as ?str_type)
	}


	VALUES (?item) {
		( ?handle_url )
	}
}